plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        //mixinConfig "frightsdelight-common.mixins.json" // for future use
        mixinConfig "frightsdelight.mixins.json"
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    //modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

    // Farmer's Delight
    modImplementation "curse.maven:farmers-delight-398521:${farmersdelight_version}"

    // MidnightLib
    modImplementation include("maven.modrinth:midnightlib:${project.midnightlib_forge_version}")

    // Appleskin
    modCompileOnly("squeek.appleskin:appleskin-forge:${appleskin_forge_version}:api")
    modRuntimeOnly("squeek.appleskin:appleskin-forge:${appleskin_forge_version}")

    // Recipe Viewers
    // EMI
    modCompileOnly("dev.emi:emi-forge:$emi_version+${minecraft_version}:api") { transitive = false }
    // JEI
    modCompileOnly("mezz.jei:jei-$minecraft_version-common-api:$jei_version") { transitive = false }
    modCompileOnly("mezz.jei:jei-$minecraft_version-forge-api:$jei_version") { transitive = false }
    // REI
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-forge:$rei_version") { transitive = false }
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-default-plugin-forge:$rei_version")

    switch (recipe_viewer.toLowerCase(Locale.ROOT)) {
        case "jei": modLocalRuntime("mezz.jei:jei-${minecraft_version}-forge:${jei_version}"); break
        case "rei": modLocalRuntime("me.shedaniel:RoughlyEnoughItems-forge:${rei_version}"); break
        case "emi": modLocalRuntime("dev.emi:emi-forge:${emi_version}+${minecraft_version}"); break
        case "disabled": break
        default: println("Unknown recipe viewer specified: ${recipe_viewer}. Must be JEI, REI, EMI, or disabled.")
    }

    // WTHIT
    modCompileOnly("mcp.mobius.waila:wthit-api:forge-${wthitVersion}")
    modLocalRuntime("mcp.mobius.waila:wthit:forge-${wthitVersion}")
    modLocalRuntime("lol.bai:badpackets:forge-${badpacketsVersion}")

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

processResources {

}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

var env = System.getenv()

var changelogFile = rootProject.file("changelog.md")
var changelogText = changelogFile.exists() ? changelogFile.text : ""
var vName = mod_name + " " + project.version

if (env.MODRINTH_TOKEN) {
    modrinth {
        token = System.getenv("MODRINTH_TOKEN") // This is the default. Remember to have the MODRINTH_TOKEN environment variable set or else this will fail, or set it to whatever you want - just make sure it stays private!
        projectId = modrinth_project_id // This can be the project ID or the slug. Either will work!
        versionName = vName
        versionNumber = project.version // You don't need to set this manually. Will fail if Modrinth has this version already
        versionType = mod_version_type // This is the default -- can also be `beta` or `alpha`
        changelog = changelogText
        uploadFile = remapJar // With Loom, this MUST be set to `remapJar` instead of `jar`!
        gameVersions = ["1.20.1"]
        loaders = ["forge","neoforge"]
        dependencies {
            // scope.type
            // The scope can be `required`, `optional`, `incompatible`, or `embedded`
            // The type can either be `project` or `version`
            required.project "farmers-delight"
            embedded.project "midnightlib"
            optional.project "jei"
            optional.project "rei"
            optional.project "emi"
        }
    }
}

if (env.CURSEFORGE_TOKEN) {
    curseforge {
        apiKey = env.CURSEFORGE_TOKEN
        project {
            id = curseforge_project_id
            changelog = changelogText
            changelogType = 'markdown'
            releaseType = mod_version_type
            addGameVersion "1.20.1"
            addGameVersion "Forge"
            addGameVersion "NeoForge"

            mainArtifact(remapJar) {
                displayName = vName
            }

            relations {
                requiredDependency "farmers-delight"
                embeddedLibrary "midnightlib"
                optionalDependency "jei"
                optionalDependency "roughly-enough-items"
                optionalDependency "emi"
            }
        }
    }
}